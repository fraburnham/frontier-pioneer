module Map exposing (map)

import Html exposing (Html)
import Html.Attributes as Attribute exposing (class, title, id)
import Html.Events exposing (onClick)

import Types exposing (..)
import Rules exposing (..)


sectorStyleMapped : Model -> Sector -> String
sectorStyleMapped model s =
    when s is
        Unmapped -> "divide-gray-200"
        Mapped _ -> "divide-gray-400 hover:font-medium"


sectorStyleCurrentLocation : Model -> Int -> Int -> String
sectorStyleCurrentLocation model col row =
    when model.location is
        Nothing -> ""
        Just l ->
            when (l.col == col && l.row == row) is
                True -> "bg-sky-100"
                False -> ""


sectorStyleValidForAction : Model -> Int -> Int -> Sector -> String
sectorStyleValidForAction model col row s =
    let
        validStyle = "bg-gray-100"
        invalidStyle = ""
        coords = { row = row, col = col }
    in
    when model.location is
        Nothing -> validStyle
        Just l ->
            when model.turnState is
                Nothing -> invalidStyle
                Just t ->
                    let
                        checkAction = (\action ->
                                          when action is
                                              Move ml ->
                                                  when (validMove ml l coords) is
                                                      True -> validStyle
                                                      False -> invalidStyle

                                              MapSector ->
                                                  when (validMapSector t.roll.d10 s l coords) is
                                                      True -> validStyle
                                                      False -> invalidStyle

                                              ResourceScan ->
                                                  when (validResourceScan t.roll.d10 s l coords) is
                                                      True -> validStyle
                                                      False -> invalidStyle

                                              _ -> invalidStyle
                                      )
                    in
                    when model.hoveredAction is
                        Just ha -> checkAction ha
                        Nothing ->
                            when t.action is
                                NoAction -> invalidStyle
                                a -> checkAction a


sectorStyle : Model -> Int -> Int -> Sector -> String
sectorStyle model col row s =
    String.join " "
      [ "flex flex-col size-fit divide-y-1"
      , sectorStyleMapped model s
      , sectorStyleCurrentLocation model col row
      , sectorStyleValidForAction model col row s
      ]


sectorRowStyle : Sector -> String
sectorRowStyle s =
    when s is
        Unmapped -> "flex flex-row divide-x-1 divide-gray-200"
        Mapped _ -> "flex flex-row divide-x-1 divide-gray-400"


sectorBoxStyle : String
sectorBoxStyle = "h-[1.5rem] w-[1.5rem] flex items-center justify-center"


sector : Model -> Int -> Int -> Sector -> Html Msg
sector model row col data =
    let
        sectorRowStyleResolved = sectorRowStyle data
        sectorAttributes = [ class (sectorStyle model col row data)
                           , onClick (SectorClicked { row = row
                                                    , col = col }
                                     )
                           ]
    in
    when data is
        Unmapped ->
            Html.div sectorAttributes
              [ Html.div [ class sectorRowStyleResolved ]
                  [ Html.div [ class sectorBoxStyle ]
                      []
                  , Html.div [ class sectorBoxStyle ]
                      []
                  ]
              , Html.div [ class sectorRowStyleResolved ]
                  [ Html.div [ class sectorBoxStyle ]
                      []
                  , Html.div [ class sectorBoxStyle ]
                      []
                  ]
              ]

        Mapped d ->
            Html.div sectorAttributes
              [ Html.div [ class sectorRowStyleResolved ]
                  [ Html.div [ class sectorBoxStyle ]
                      []
                  , Html.div [ class sectorBoxStyle
                             , title (sectorKindToName d.kind) ]
                      [ Html.text (sectorKindToSymbol d.kind) ]
                  ]
              , when d.resource is
                    Undiscovered ->
                        Html.div [ class sectorRowStyleResolved ]
                          [ Html.div [ class sectorBoxStyle ]
                              []
                          , Html.div [ class sectorBoxStyle ]
                              []
                          ]

                    Discovered rd ->
                        Html.div [ class sectorRowStyleResolved ]
                          [ Html.div [ class sectorBoxStyle ]
                              [ Html.text (String.fromInt rd.count) ]
                          , Html.div [ class sectorBoxStyle
                                     , title (resourceKindToName rd.kind)
                                     ]
                              [ Html.text (resourceKindToSymbol rd.kind) ]
                          ]
              ]


mapRow : Model -> Int -> Array(Sector) -> Html Msg
mapRow model row sectors =
    Html.div [ class "flex flex-row divide-x-1 size-fit" ]
      (Array.indexedMap (sector model row) sectors)


map : Model -> Html Msg
map model =
    Html.div [ id "map"
             , class "flex flex-col border-1 divide-y-1 size-fit m-2 bg-sky-50"
             ]
      (Array.indexedMap (mapRow model) model.sectors)

module Update exposing (update)

import Random

import Types exposing (..)
import Rules exposing (validMove)


rollDice : Cmd Msg
rollDice =
    Random.generate Rolled
      (Random.constant (\d4 d6 d8 d10 d12 d20 -> { d4 = d4
                                                 , d6 = d6
                                                 , d8 = d8
                                                 , d10 = d10
                                                 , d12 = d12
                                                 , d20 = d20
                                                 }
                       )
       |> Random.andMap (Random.int 1 4)
       |> Random.andMap (Random.int 1 6)
       |> Random.andMap (Random.int 1 8)
       |> Random.andMap (Random.int 0 9)
       |> Random.andMap (Random.int 1 12)
       |> Random.andMap (Random.int 1 20)
      )


sectorClicked : Model -> Coordinates -> Model
sectorClicked model coords =
    when model.location is
        Nothing ->
            { model | location = Just coords }

        Just l ->
            when model.turnState is
                Nothing ->
                    model

                Just t ->
                    when t.action is
                        NoAction ->
                            model

                        Move movesLeft ->
                            when (validMove movesLeft l coords) is
                                True ->
                                    { model
                                    | location = Just coords
                                    , turnState = ( when movesLeft is
                                                        1 -> Nothing
                                                        _ -> Just { t | action = Move (movesLeft - 1) }
                                                  )
                                    }

                                False ->
                                    model

                        _ ->
                            model


moveSpaces : Int -> Model -> Model
moveSpaces movesLeft model =
    when model.turnState is
        Nothing ->
            model

        Just t ->
            { model | turnState = Just { t | action = (Move movesLeft) } }


update : Msg -> Model -> { model : Model, command : Cmd Msg }
update msg model =
    when (Debug.log "Message" msg) is
        SectorClicked c ->
            { model = sectorClicked model c
            , command = Cmd.none
            }

        RollDice ->
            { model = model
            , command = rollDice
            }

        Rolled result ->
            { model = { model | turnState = Just { roll = result
                                                 , action = NoAction
                                                 }
                      }
            , command = Cmd.none
            }

        MoveSpaces movesLeft ->
            { model = moveSpaces movesLeft model
            , command = Cmd.none
            }

module Update exposing (update)

import Array
import Random

import Types exposing (..)
import Rules exposing (..)


rollDice : Cmd Msg
rollDice =
    Random.generate Rolled
      (Random.constant (\d4 d6 d8 d10 d12 d20 -> { d4 = d4
                                                 , d6 = d6
                                                 , d8 = d8
                                                 , d10 = d10
                                                 , d12 = d12
                                                 , d20 = d20
                                                 }
                       )
       |> Random.andMap (Random.int 1 4)
       |> Random.andMap (Random.int 1 6)
       |> Random.andMap (Random.int 1 8)
       |> Random.andMap (Random.int 0 9)
       |> Random.andMap (Random.int 1 12)
       |> Random.andMap (Random.int 1 20)
      )


getSector : Model -> Coordinates -> Maybe Sector
getSector model coords =
    when (Array.get coords.row model.sectors) is
        Nothing -> Nothing

        Just row ->
            Array.get coords.col row


mapSector : TurnState -> Sector -> Sector
mapSector t sector =
    when sector is
        -- This should be unreachable due to validMapSector
        Mapped s -> Mapped s

        Unmapped ->
            Mapped { kind = intToSectorKind t.roll.d6
                   , resource = Undiscovered
                   }


resourceScan : TurnState -> Sector -> Sector
resourceScan t sector =
    when sector is
        -- This should be unreachable due to validResourceScan
        Unmapped -> Unmapped

        Mapped s ->
            when s.resource is
                -- This should be unreachable due to validResourceScan
                Discovered _ -> Mapped s

                Undiscovered ->
                    Mapped { s | resource = Discovered { kind = intToResourceKind t.roll.d8
                                                       , count = t.roll.d12
                                                       }
                           }


updateSector : (Sector -> Sector) -> Array (Array Sector) -> Coordinates -> Array (Array Sector)
updateSector updateFn arr coords =
    Array.update
      coords.row
      (\row -> Array.update coords.col updateFn row)
      arr


sectorClicked : Model -> Coordinates -> Model
sectorClicked model coords =
    when model.location is
        Nothing -> { model | location = Just coords }

        Just l ->
            when model.turnState is
                Nothing -> model

                Just t ->
                    when t.action is
                        NoAction -> model

                        Move movesLeft ->
                            when (validMove movesLeft l coords) is
                                True ->
                                    { model
                                    | location = Just coords
                                    , turnState = ( when movesLeft is
                                                        1 -> Nothing
                                                        _ -> Just { t | action = Move (movesLeft - 1) }
                                                  )
                                    }

                                False ->
                                    model

                        MapSector ->
                            when (getSector model coords) is
                                Nothing -> model
                                Just s ->
                                    when (validMapSector t.roll.d10 s l coords) is
                                        True ->
                                            { model
                                            | sectors = updateSector (mapSector t) model.sectors coords
                                            , turnState = Nothing
                                            }

                                        _ -> model

                        ResourceScan ->
                            when (getSector model coords) is
                                Nothing -> model
                                Just s ->
                                    when (validResourceScan t.roll.d10 s l coords) is
                                        False -> model
                                        True ->
                                            { model
                                            | sectors = updateSector (resourceScan t) model.sectors coords
                                            , turnState = Nothing
                                            }

                        Anomaly -> model


updateTurnStateAction : Action -> Model -> Model
updateTurnStateAction action model =
    when model.turnState is
        Nothing ->
            model

        Just t ->
            { model | turnState = Just { t | action = action } }


update : Msg -> Model -> { model : Model, command : Cmd Msg }
update msg model =
    when (Debug.log "Message" msg) is
        SectorClicked c ->
            { model = sectorClicked model c
            , command = Cmd.none
            }

        RollDice ->
            { model = model
            , command = rollDice
            }

        Rolled result ->
            { model = { model | turnState = Just { roll = result
                                                 , action = NoAction
                                                 }
                      }
            , command = Cmd.none
            }

        SelectedAction sa ->
            { model = updateTurnStateAction sa model
            , command = Cmd.none
            }

